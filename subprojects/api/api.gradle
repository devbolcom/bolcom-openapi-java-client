import static org.gradle.api.JavaVersion.VERSION_1_7

apply plugin: 'java'
apply plugin: 'groovy'
apply from: script('integrationTest')

description = 'Bol.com OpenAPI v4 HTTP Client'

targetCompatibility = VERSION_1_7
sourceCompatibility = VERSION_1_7

dependencies {
    compile project(':schema')
    compile libraries.feign
    compile libraries.apacheCommonsLang

    testCompile libraries.spock
    testCompile libraries.hamcrest
    testCompile libraries.mockWebServer
}

def setTestProperties = {
    def openApiProps = new Properties()
    openApiProps.load(rootProject.file('openapi.properties').newInputStream())
    systemProperty('OPENAPI_KEY', openApiProps.getProperty('test.openapi.v4.apikey'))
}

test setTestProperties
integrationTest setTestProperties

apply from: script('publishing')

archivesBaseName = "${rootProject.name}-${project.name}"
publishing {
    publications {
        main(MavenPublication) {
            artifactId project.archivesBaseName
        }
    }
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.outputs.files
}
task groovydocJar(type: Jar) {
    classifier = 'groovydoc'
    from groovydoc.outputs.files
}
artifacts {
    archives sourcesJar
//    archives javadocJar
//    archives groovydocJar
}
