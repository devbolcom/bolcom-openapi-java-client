ext {
    libraries = [:]
    versions = [:]
}

versions += [
        groovy    : '2.3.6',
        feign     : '6.1.2',
]

libraries += [
        groovy           : "org.codehaus.groovy:groovy:${versions.groovy}",
        groovyJson       : "org.codehaus.groovy:groovy-json:${versions.groovy}",
        apacheCommonsLang: "org.apache.commons:commons-lang3:3.3.1",

        junit            : 'junit:junit:4.11',
        asm              : 'org.ow2.asm:asm:4.1',
        hamcrest         : 'org.hamcrest:hamcrest-library:1.3',
        mockWebServer    : "com.squareup.okhttp:mockwebserver:1.5.3",
]

libraries.feign = ["com.netflix.feign:feign-core:${versions.feign}",
                   "com.netflix.feign:feign-jackson:${versions.feign}",
                   "com.netflix.feign:feign-jaxrs:${versions.feign}",
]

libraries.spock = ['org.spockframework:spock-core:0.7-groovy-2.0',
                   libraries.groovy,
                   'org.objenesis:objenesis:2.0',
                   'cglib:cglib-nodep:2.2'
]

allprojects {
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def req = details.requested
            // Replace groovy-all with groovy
            if (req.name == 'groovy-all' || req.name == 'groovy') {
                details.useTarget libraries.groovy
            }
            // Replace junit-dep with junit
            if (req.name == 'junit-dep') {
                details.useTarget libraries.junit
            }
            // Change old groupId of asm to new groupId, for correct dependency handling
            if (req.name == 'asm') {
                def (group, name, version) = libraries.asm.split(':')
                details.useTarget group: group, name: name, version: version
            }
        }
    }
}
